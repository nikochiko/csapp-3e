# 3.66

## Solution

Annotating the assembly code,

```
	long sum_col(long n, long A[NR(n)][NC(n)], long j)
	n in %rdi, A in %rsi, j in %rdx
1	sum_col:
2	  leaq	1(,%rdi,4), %r8			%r8 <- 4n + 1
3	  leaq	(%rdi,%rdi,2), %rax		%rax <- 3n
4	  movq	%rax, %rdi				%rdi <- %rax
5	  testq	%rax, %rax				like %rax & %rax
6	  jle	.L4						%rax <= 0 ?
7	  salq	$3, %r8					%r8 *= 8
8	  leaq	(%rsi,%rdx,8), %rcx		ptr = A + 8*j
9	  movl	$0, %eax				%rax <- 0
10	  movl	$0, %edx				%rdx <- 0
11	.L3:
12	  addq	(%rcx), %rax			result += *ptr
13	  addq	$1, %rdx				%rdx += 1 (%rdx is i)
14	  addq	%r8, %rcx				ptr += (4n+1)*8
15	  cmpq	%rdi, %rdx				compare i:3n
16	  jne	.L3						if i != 3n, loop
17	  rep;	ret
18	.L4:
19	  movl	$0, %eax				result = 0
20	  ret
```

From lines 10, 13 and 15, we can see that `i` goes from
0 to 3n with a step of 1. From the stopping condition, we
get that `NR(n)` must be `3n`.

On line 14, we can see that the pointer is being
moved ahead by $(4n+1) \cdot 8$ bytes on each iteration.
Because 8 is the number of bytes taken by a `long`,
we get that $4n+1$ is the number of columns in the array.

`NR(n) = 3n`

`NC(n) = 4n + 1`
